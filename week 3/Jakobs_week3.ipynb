{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "constructBnodes (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using LinearAlgebra\n",
    "using SparseArrays\n",
    "using Plots\n",
    "\n",
    "function xy(\n",
    "    x0::Float64, \n",
    "    y0::Float64, \n",
    "    L1::Float64, \n",
    "    L2::Float64, \n",
    "    noelms1::Int64, \n",
    "    noelms2::Int64\n",
    ")::Tuple{Vector{Float64}, Vector{Float64}}\n",
    "    VX = repeat(collect(LinRange(x0, x0+L1, noelms1+1)), inner=noelms2+1)\n",
    "    VY = repeat(collect(LinRange(y0+L2, y0, noelms2+1)), noelms1+1)\n",
    "    return VX, VY\n",
    "end\n",
    "\n",
    "function conelmtab(noelms1::Int64, noelms2::Int64)::Matrix{Int64}\n",
    "    k = [i for i in 1:(noelms1*(noelms2+1)) if i % (noelms2+1) != 0]\n",
    "\n",
    "    return [\n",
    "        k (2 + noelms2 .+ k) (1 + noelms2 .+ k);\n",
    "        k (1 .+ k) (2 + noelms2 .+ k)\n",
    "    ]\n",
    "end\n",
    "\n",
    "function basfun(\n",
    "    VX::Vector{Float64}, \n",
    "    VY::Vector{Float64}, \n",
    "    EToV::Matrix{Int64}\n",
    ")::Tuple{Matrix{Float64}, Matrix{Float64}, Matrix{Float64}}\n",
    "    xj = VX[EToV[:, [2,3,1]]]\n",
    "    yj = VY[EToV[:, [2,3,1]]]\n",
    "\n",
    "    xk = VX[EToV[:, [3,1,2]]]\n",
    "    yk = VY[EToV[:, [3,1,2]]]\n",
    "\n",
    "    at = xj .* yk - xk .* yj\n",
    "    bt = yj - yk\n",
    "    ct = xk - xj\n",
    "\n",
    "    return at, bt, ct\n",
    "end\n",
    "\n",
    "function constructBeds(\n",
    "    VX::Vector{Float64},\n",
    "    VY::Vector{Float64},\n",
    "    EToV::Matrix{Int64},\n",
    "    tol::Float64,\n",
    "    fd::Function,\n",
    ")::Matrix{Int64}\n",
    "    xc = (VX[EToV] + VX[EToV[:, [2, 3, 1]]]) ./ 2\n",
    "    yc = (VY[EToV] + VY[EToV[:, [2, 3, 1]]]) ./ 2\n",
    "    return getindex.(findall(abs.(fd.(xc, yc)) .<= tol), [1 2])\n",
    "end\n",
    "\n",
    "function constructBnodes(\n",
    "    VX::Vector{Float64},\n",
    "    VY::Vector{Float64},\n",
    "    tol::Float64,\n",
    "    fd::Function,\n",
    ")::Vector{Int64}\n",
    "    return findall(abs.(fd.(VX, VY)) .<= tol)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve_square_time_depedent_dirchlet_BVP (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function assembly(\n",
    "    VX::Vector{Float64},\n",
    "    VY::Vector{Float64},\n",
    "    EToV::Matrix{Int64},\n",
    "    lam1::Float64,\n",
    "    lam2::Float64,\n",
    "    qt::Vector{Float64},\n",
    "    c::Vector{Float64}\n",
    ")::Tuple{SparseMatrixCSC{Float64, Int64}, SparseMatrixCSC{Float64, Int64}, Vector{Float64}}\n",
    "    N = size(EToV)[1]\n",
    "    M = length(VX)\n",
    "\n",
    "    A = spzeros(M, M)\n",
    "    C = spzeros(M, M)\n",
    "    b = zeros(M)\n",
    "\n",
    "    at, bt, ct = basfun(VX, VY, EToV)\n",
    "\n",
    "    for n in 1:N\n",
    "        abs_delta_n = abs(sum(at[n, :]) / 2)\n",
    "        btn = bt[n, :]\n",
    "        ctn = ct[n, :]\n",
    "\n",
    "        EToV_n = EToV[n,:]\n",
    "        qtn = abs_delta_n * sum(qt[EToV_n]) / 9\n",
    "        cn = sum(c[EToV_n]) / 3\n",
    "\n",
    "        for r in 1:3\n",
    "            i = EToV_n[r]\n",
    "            b[i] += qtn\n",
    "\n",
    "            for s in r:3\n",
    "                j = EToV_n[s]\n",
    "\n",
    "                ij_min = min(i, j)\n",
    "                ij_max = max(i, j)\n",
    "                \n",
    "                A[ij_min, ij_max] += (lam1*btn[r]*btn[s] + lam2*ctn[r]*ctn[s]) / (4 * abs_delta_n)\n",
    "                C[ij_min, ij_max] += cn * abs_delta_n * (1 + (r == s)) / 12\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return A, C, b\n",
    "end\n",
    "\n",
    "function dirbc(\n",
    "    bnodes::Vector{Int64}, \n",
    "    f::Vector{Float64}, \n",
    "    A::SparseMatrixCSC{Float64, Int64}, \n",
    "    C::SparseMatrixCSC{Float64, Int64}, \n",
    "    b::Vector{Float64}\n",
    ")::Tuple{SparseMatrixCSC{Float64, Int64}, SparseMatrixCSC{Float64, Int64}, Vector{Float64}, Vector{Float64}}\n",
    "    d = zeros(length(b))\n",
    "\n",
    "    for (i, k) in enumerate(bnodes)\n",
    "        b[k] = 0\n",
    "        A[k, k] = 0\n",
    "        C[k, k] = 1\n",
    "\n",
    "        indices1 = findall(A[1:k-1, k] .!= 0)\n",
    "        indices2 = findall(A[k, 1+k:end] .!= 0) .+ k\n",
    "\n",
    "        temp1 = A[indices1,k] .* f[i]\n",
    "        temp2 = A[k,indices2] .* f[i]\n",
    "\n",
    "        b[indices1] -= temp1\n",
    "        b[indices2] -= temp2\n",
    "\n",
    "        d[indices1] += temp1\n",
    "        d[indices2] += temp2\n",
    "\n",
    "        A[indices1, k] .= 0\n",
    "        A[k, indices2] .= 0\n",
    "\n",
    "        indices1 = findall(C[1:k-1, k] .!= 0)\n",
    "        indices2 = findall(C[k, 1+k:end] .!= 0) .+ k\n",
    "\n",
    "        C[indices1, k] .= 0\n",
    "        C[k, indices2] .= 0\n",
    "    end\n",
    "\n",
    "    return A, C, b, d\n",
    "end\n",
    "\n",
    "function solve_time_depedent_dirchlet_BVP(\n",
    "    VX::Vector{Float64},\n",
    "    VY::Vector{Float64},\n",
    "    EToV::Matrix{Int64},\n",
    "    lam1::Float64,\n",
    "    lam2::Float64,\n",
    "    qt::Function,\n",
    "    c::Function,\n",
    "    f::Function,\n",
    "    fd_gamma::Function,\n",
    "    tol::Float64,\n",
    "    theta::Float64,\n",
    "    u0::Vector{Float64},\n",
    "    dt::Float64,\n",
    "    N::Int64\n",
    ")::Matrix{Float64}\n",
    "    R, S, b = assembly(VX, VY, EToV, lam1, lam2, qt.(VX, VY), c.(VX, VY))\n",
    "\n",
    "    bnodes = constructBnodes(VX, VY, tol, fd_gamma)\n",
    "    R, S, b = dirbc(bnodes, f.(VX[bnodes], VY[bnodes]), R, S, b)\n",
    "\n",
    "    M = length(VX)\n",
    "    u = zeros(N+1, M)\n",
    "    u[1, :] = u0\n",
    "\n",
    "    d1 = dt * theta\n",
    "    d2 = dt * (1 - theta)\n",
    "    S -= d2 * R\n",
    "    R = S + dt * R\n",
    "\n",
    "    S = Symmetric(S)\n",
    "    R = Symmetric(R)\n",
    "\n",
    "    for n in 1:N\n",
    "        e = S * u[n, :]\n",
    "        u[n+1, :] = R \\ e\n",
    "    end\n",
    "\n",
    "    return u\n",
    "end\n",
    "\n",
    "function solve_square_time_depedent_dirchlet_BVP(\n",
    "    x0::Float64, \n",
    "    y0::Float64, \n",
    "    L1::Float64, \n",
    "    L2::Float64, \n",
    "    noelms1::Int64, \n",
    "    noelms2::Int64, \n",
    "    lam1::Float64,\n",
    "    lam2::Float64,\n",
    "    qt::Function,\n",
    "    c::Function,\n",
    "    f::Function,\n",
    "    theta::Float64,\n",
    "    u0::Function,\n",
    "    dt::Float64,\n",
    "    N::Int64\n",
    ")::Tuple{Vector{Float64}, Vector{Float64}, Matrix{Float64}}\n",
    "    fd_gamma(x, y) = min(x - x0, y - y0, x0 + L1 - x, y0 + L2 - y)\n",
    "    tol = 0.0001\n",
    "\n",
    "    VX, VY = xy(x0, y0, L1, L2, noelms1, noelms2)\n",
    "    EToV = conelmtab(noelms1, noelms2)\n",
    "\n",
    "    u = solve_time_depedent_dirchlet_BVP(VX,VY,EToV,lam1,lam2,qt,c,f,fd_gamma,tol,theta,u0.(VX, VY),dt,N)\n",
    "    \n",
    "    return VX, VY, u\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "x0 = 0.0\n",
    "y0 = 0.0\n",
    "L1 = 1.0\n",
    "L2 = 1.0\n",
    "noelms1 = 20\n",
    "noelms2 = 20\n",
    "lam1 = 1.0\n",
    "lam2 = 1.0\n",
    "qt(x,y) = 0.0\n",
    "c(x,y) = 1.0\n",
    "f(x, y) = 0.0\n",
    "u0(x, y) = sin(pi * x) * sin(pi * y)\n",
    "theta = 0.0\n",
    "dt = 0.001\n",
    "N = 100\n",
    "\n",
    "VX, VY, u = solve_square_time_depedent_dirchlet_BVP(\n",
    "    x0,\n",
    "    y0,\n",
    "    L1,\n",
    "    L2,\n",
    "    noelms1,\n",
    "    noelms2,\n",
    "    lam1,\n",
    "    lam2,\n",
    "    qt,\n",
    "    c,\n",
    "    f,\n",
    "    theta,\n",
    "    u0,\n",
    "    dt,\n",
    "    N\n",
    ")\n",
    "\n",
    "anim = @animate for i in 1:size(u)[1]\n",
    "    plot(VX, VY, u[i, :], st=:surface, zlim=(0,1), clim=(0,1))\n",
    "end\n",
    "\n",
    "gif(anim, fps=10)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
